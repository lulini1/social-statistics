DATA LIST NOTABLE FILE='C:\EHA_SPSS\EX5.DAT' RECORDS=3
  /2 D1 TO D60 (60F1.0).

FLIP VARIABLES=D1 TO D60.
DO REPEAT R=VAR001 TO VAR004 / D=CASE1 TO CASE4.
COMPUTE D=0.
DO IF (R NE 0).
COMPUTE D=$CASENUM.
END IF.
IF (R=0) D=61.
END REPEAT.

FLIP VARIABLES=CASE1 TO CASE4.
COMPUTE ID=$CASENUM.
COMPUTE DURA=MIN(D1 TO D60).
SAVE OUTFILE='C:\EHA_SPSS\TEM1' /KEEP=ID DURA.

DATA LIST NOTABLE FILE='C:\EHA_SPSS\EX5.DAT' RECORDS=3
   /1 ID 1-3 AGE 5-6 FUNDS 8 PRES 10
   /3 #P1 TO #P60 (60F1.0).

COMPUTE PUB1=#P7.
COMPUTE PUB2=#P19.
COMPUTE PUB3=#P31.
COMPUTE PUB4=#P43.
COMPUTE PUB5=#P55.

MATCH FILES FILE=* /FILE='C:\EHA_SPSS\TEM1' /BY=ID.
SAVE OUTFILE='C:\EHA_SPSS\TEM2'.

DO REPEAT A=AGE1 TO AGE5 /F=FUNDS1 TO FUNDS5
   /P=PRES1 TO PRES5 /C=CHANGE1 TO CHANGE5.
COMPUTE A=AGE.
COMPUTE F=FUNDS.
COMPUTE P=PRES.
COMPUTE C=0.
END REPEAT.
EXECUTE.

IF (DURA GE 1 AND DURA LE 12) CHANGE1=1.
IF (DURA GT 12 AND DURA LE 24) CHANGE2=1.
IF (DURA GT 24 AND DURA LE 36) CHANGE3=1.
IF (DURA GT 36 AND DURA LE 48) CHANGE4=1.
IF (DURA GT 48 AND DURA LE 60) CHANGE5=1.

DO IF(CHANGE1=1).
DO REPEAT X=CHANGE2 TO CHANGE5.
COMPUTE X=-9.
END REPEAT.
ELSE IF (CHANGE2=1).
DO REPEAT X=CHANGE3 TO CHANGE5.
COMPUTE X=-9.
END REPEAT.
ELSE IF (CHANGE3=1).
DO REPEAT X=CHANGE4 TO CHANGE5.
COMPUTE X=-9.
END REPEAT.
ELSE IF (CHANGE4=1).
COMPUTE CHANGE5=-9.
END IF.

COMPUTE ORDER1=1.
COMPUTE ORDER2=2.
COMPUTE ORDER3=3.
COMPUTE ORDER4=4.
COMPUTE ORDER5=5.

WRITE FORMATS ID (F4.0)
   /AGE1 TO AGE5 (F2.0)
   /FUNDS1 TO FUNDS5
    PRES1 TO PRES5
    CHANGE1 TO CHANGE5
    PUB1 TO PUB5
    ORDER1 TO ORDER5 (F2.0).
WRITE OUTFILE='C:\EHA_SPSS\OUT1' RECORDS=5
   /1 ID AGE1 ORDER1 FUNDS1 PRES1 CHANGE1 PUB1
   /2 ID AGE2 ORDER2 FUNDS2 PRES2 CHANGE2 PUB2
   /3 ID AGE3 ORDER3 FUNDS3 PRES3 CHANGE3 PUB3
   /4 ID AGE4 ORDER4 FUNDS4 PRES4 CHANGE4 PUB4
   /5 ID AGE5 ORDER5 FUNDS5 PRES5 CHANGE5 PUB5.

EXECUTE.

DATA LIST NOTABLE FILE='C:\EHA_SPSS\OUT1'
   /1 ID 1-4 AGE 5-6 ORDER FUNDS PRES CHANGE PUB (5F2.0).
SORT CASES BY ORDER.
SAVE OUTFILE='C:\EHA_SPSS\TEM3'.

MATRIX.
COMPUTE M=IDENT(5).
SAVE M /OUTFILE='C:\EHA_SPSS\TEM4'.
END MATRIX.
GET FILE='C:\EHA_SPSS\TEM4'.
COMPUTE ORDER=$CASENUM.
RENAME VARIABLES (COL1 TO COL5=YR1 TO YR5).
MATCH FILES TABLE=* /FILE='C:\EHA_SPSS\TEM3' /BY=ORDER.

SORT CASES BY ID.

SELECT IF NOT (CHANGE=-9).

LIST VAR=ID CHANGE AGE FUNDS PRES PUB YR1 TO YR5.

LOGISTIC REGRESSION VARIABLES=CHANGE WITH AGE
  FUNDS PRES PUB
     /EXTERNAL.
SAVE OUTFILE='C:\EHA_SPSS\FINAL.SAV'.


